#
# SPDX-FileCopyrightText: (C) Alex Smith <alex@alex-smith.me.uk>
# SPDX-License-Identifier: ISC
#

from util import feature_sources
Import('config', 'kern_env', 'kern_sources', 'mod_env')

kern_sources += feature_sources(config, [
    'ap_boot.bin.c',
    'acpi.c',
    'arch.c',
    'console.c',
    'cpu.c',
    'descriptor.c',
    'elf.c',
    'entry.S',
    'interrupt.c',
    'kdb.c',
    'lapic.c',
    'mmu.c',
    'page.c',
    'pic.c',
    'pit.c',
    'rtc.c',
    'setjmp.S',
    'smp.c',
    'switch.S',
    'thread.c',
    'time.c',
])

for env in [kern_env, mod_env]:
    # Prevent FPU code generation.
    env['CCFLAGS'] += ['-mno-sse', '-mno-mmx', '-mno-sse2', '-mno-3dnow']

    # Warn on stack frames that are too large.
    env['CCFLAGS'] += ['-Wframe-larger-than=512']

    # Set other AMD64-specific flags.
    env['CCFLAGS']   += ['-mcmodel=kernel', '-mno-red-zone']
    env['LINKFLAGS'] += ['-Wl,-z,max-page-size=0x1000', '-Wl,-z,common-page-size=0x1000']

# Set the linker script path.
kern_env['LDSCRIPT'] = kern_env.LDScript('link.ld', 'link.ld.in')

# Generate the AP boot code.
kern_env.Command(
    'ap_boot.bin',
    kern_env.Object('ap_boot.S'),
    Action('$LD -Ttext=0 --oformat binary --strip-all -o $TARGET $SOURCE', None))
kern_env.Command(
    'ap_boot.bin.c',
    ['ap_boot.bin'],
    Action('$BIN2HEX --no-size --section .init.trampoline $SOURCE __smp_trampoline > $TARGET', '$GENCOMSTR'))
