#
# SPDX-FileCopyrightText: (C) Alex Smith <alex@alex-smith.me.uk>
# SPDX-License-Identifier: ISC
#

Import('config', 'manager')

env = manager.create_host()

# Warnings that occur in generated code.
env['CCFLAGS'] += ['-Wno-redundant-decls', '-Wno-unused-function']

sysgen = env.Program('sysgen', [
    'amd64_target.cpp',
    'arm64_target.cpp',
    'parser.y',
    'lexer.l',
    'main.cpp',
])

# Add sysgen to the build environments.
manager.add_variable('SYSGEN', sysgen)
manager.add_variable('SYSGENFLAGS', [])

# Add builders to generate system call code.
manager.add_tool('SyscallTable', env['SYSGEN'], Action(
    '$SYSGEN $SYSGENFLAGS -t $TABLE -o $TARGET %s $SOURCES' % (config['ARCH']),
    '$GENCOMSTR'))
manager.add_tool('SyscallHeader', env['SYSGEN'], Action(
    '$SYSGEN $SYSGENFLAGS -n $GUARD -o $TARGET %s $SOURCES' % (config['ARCH']),
    '$GENCOMSTR'))
manager.add_tool('Syscalls', env['SYSGEN'], Action(
    '$SYSGEN $SYSGENFLAGS -o $TARGET %s $SOURCES' % (config['ARCH']),
    '$GENCOMSTR'))
